apply plugin: 'com.android.application'

repositories {
    jcenter()
    maven { url "https://maven.google.com" }
    maven { url "https://jitpack.io" }
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    defaultConfig {
        applicationId "de.wirecard.accept.sample"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 14
        versionName "1.14.0"
    }
    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/com.fasterxml.jackson.core.JsonFactory'

    }

    productFlavors {
        flavor_thyron {
            applicationId 'de.wirecard.accept.sample.thyron'
        }

        flavor_thyronUsb {
            applicationId 'de.wirecard.accept.sample.usb'
        }

        flavor_bbpos {
            applicationId 'de.wirecard.accept.sample.bbpos'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        all {
            def Properties appProps = new Properties()
            try {
                appProps.load(new FileInputStream(file("../../AcceptSDKAndroidDemo.properties")))
            } catch (IOException e) {
                appProps.put("clientID", System.getenv('BACKEND_DEFAULT_UID'))
                appProps.put("clientSecret", System.getenv('BACKEND_DEFAULT_SECRET'))
                appProps.put("apiPath", System.getenv('BACKEND_DEFAULT_URL'))
            }

            buildConfigField "String", "clientID", appProps.getProperty("clientID", "\"\"")
            buildConfigField "String", "clientSecret", appProps.getProperty("clientSecret", "\"\"")
            buildConfigField "String", "apiPath", appProps.getProperty("apiPath", "\"\"")

        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
        }
    }

}

dependencies {
    compile 'com.github.WirecardMobileServices:acceptSDK-Android:1.6.1'
    flavor_bbposCompile 'com.github.WirecardMobileServices:accept-android-extension-bbpos:1.6.1'
    flavor_thyronCompile 'com.github.WirecardMobileServices:accept-android-extension-spire:1.6.0'
    flavor_thyronUsbCompile 'com.github.WirecardMobileServices:accept-android-extension-spire:1.6.0'

    compile 'com.android.support:appcompat-v7:25.4.0'
    compile 'com.android.support:design:25.4.0'

    compile 'org.apache.httpcomponents:httpclient-android:4.3.5'
    compile 'org.apache.httpcomponents:httpmime:4.3.5'

    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.3'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.journeyapps:zxing-android-embedded:3.5.0'
    compile 'com.scottyab:rootbeer-lib:0.0.6'
}
