apply plugin: 'com.android.application'

repositories {
    jcenter()
    maven { url "https://maven.google.com" }
    maven { url "https://jitpack.io" }
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "de.wirecard.accept.sample"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 19
        versionName "1.14.6"
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/com.fasterxml.jackson.core.JsonFactory'

    }

    useLibrary 'org.apache.http.legacy'
    flavorDimensions "terminal"

    productFlavors {
        flavor_thyron {
            applicationId 'de.wirecard.accept.sample.thyron'
            dimension "terminal"
        }

        flavor_thyronUsb {
            applicationId 'de.wirecard.accept.sample.usb'
            dimension "terminal"
        }

        flavor_bbpos {
            applicationId 'de.wirecard.accept.sample.bbpos'
            dimension "terminal"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        all {
            def Properties appProps = new Properties()
            try {
                appProps.load(new FileInputStream(file("../../AcceptSDKAndroidDemo.properties")))
            } catch (IOException e) {
                appProps.put("clientID", System.getenv('BACKEND_DEFAULT_UID'))
                appProps.put("clientSecret", System.getenv('BACKEND_DEFAULT_SECRET'))
                appProps.put("apiPath", System.getenv('BACKEND_DEFAULT_URL'))
            }

            buildConfigField "String", "clientID", appProps.getProperty("clientID", "\"\"")
            buildConfigField "String", "clientSecret", appProps.getProperty("clientSecret", "\"\"")
            buildConfigField "String", "apiPath", appProps.getProperty("apiPath", "\"\"")

        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-${variant.versionName}.apk"
        }
    }

}

dependencies {
    implementation 'com.github.WirecardMobileServices:acceptSDK-Android:1.6.5'
    flavor_bbposImplementation 'com.github.WirecardMobileServices:accept-android-extension-bbpos:1.6.3'
    flavor_thyronImplementation 'com.github.WirecardMobileServices:accept-android-extension-spire:1.6.3'
    flavor_thyronUsbImplementation 'com.github.WirecardMobileServices:accept-android-extension-spire:1.6.3'

    implementation 'com.android.support:appcompat-v7:26.0.2'
    implementation 'com.android.support:design:26.0.2'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.journeyapps:zxing-android-embedded:3.5.0'
    implementation 'com.scottyab:rootbeer-lib:0.0.6'
}
